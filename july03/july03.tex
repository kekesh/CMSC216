\section{Wednesday, July 3, 2019}
More Assembly today. Before we start, here's some quick review from the past two classes: \begin{itemize}
    \item Assembly programs are written in a \verb!.S! file.
    \item There is a \verb!.data! directive, which denotes the portion of the code where we define data (like variables).
    \item A label represents a memory address (it can be viewed as a pointer to a variable or a function). 
    \item A value in memory is defined using the \verb!.byte! directive. Hexadecimal, decimal, or binary are all permitted.
    \item The \verb!.global main! directive is like the \verb!extern! keyword in C---it indicates that the function can be accessed from outside of the current file.
    \item The \verb!lds [register], [data]! instruction stores \verb!data! into the contents of \verb!register!. Similarly, \verb!ldi [register], [constant]! allows us to load a constant value to \verb!register!. 
    \item The \verb!clr [register]! instruction clears the contents of \verb!register! to zero. 
    \item Assembly has a built-in pointer register, denoted \verb!X!, which represents the combination of registers \verb!r26! and \verb!r27!. (Why do we need two registers? Memory addresses are $16$ bits, or $2$ bytes, so they need two bytes).
    \item To initialize a pointer register like \verb!X!, we use \verb!ldi! along with \verb!lo8! and \verb!hi8! on \verb!r26! and \verb!r27!, respectively. We use these directives on whatever value we want \verb!X! to point to.
    \item To load the contents of a register pointer into another register, we can use the \verb!ld! instruction in the form \verb!ld [destination register], [register pointer]!. This is the C-equivalent of dereferencing a pointer. If we now increment \verb!r26!, we can write \verb!adiw r26, 1! to move the pointer \verb!X! by one. Using \verb!inc! would also work, but it's not great to use since with register pointers as it only operates on one register. Assembly also supports a \verb!+! operator. The instruction \verb!ld r24, X+! would load the contents of \verb!X! to \verb!r24! and move the pointer by one. 
    \item To save a value in a register, we use \verb!push! in the form \verb!push [register]! to push the contents of the register onto the stack. You need to have one \verb!pop! for every \verb!push!. 
\end{itemize}

\subsection{More on Register Pointers}
Consider the following code segment, which is an example of writing to memory:


\lstset{
caption=Assembly: Register Pointers
}
\begin{lstlisting}

;;; Example - writes the values 77 and 99 to memory using sts and st; 
;;; Also using X, Y, Z register pointers

        .data
pctd:
        .asciz "%d "

values: ; represents data memory area where we will write
        ; note we are not using any .byte      directive

        .text

.global main
main:
        call init_serial_stdio

        push r29                ; needs to save r29,r28 (callee-saved)
        push r28

        ldi r18, 77
        sts values, r18         ; assigning 77 to location values (using sts)

        ldi r28, lo8(values)    ; reading first value using Y (r29:r28)
        ldi r29, hi8(values)    ; r29:r28 = values
        ld  r24, Y+             ; using Y+ (increases pointer by one location)
        clr r25                 ; printing the value
        call pint

        ldi r18, 99             ; writing location after first entry
        st Y, r18               ; using st (NOT sts)
        ldi r30, lo8(values)    ; using Z pointer register to read value written
        ldi r31, hi8(values)    
        adiw r30, 1             ; moving forward Z pointer one position
        ld r24, Z               ; reading value written

        clr r25                 ; printing value
        call pint

        clr r25                 ; newline
        ldi r24, 0xa
        call putchar

        pop r28
        pop r29

           cli ; stopping program
        sleep

        ret

pint:
        ;; prints an integer value, r22/r23 have the format string
        ldi r22, lo8(pctd)      ; lower byte of the string address
        ldi r23, hi8(pctd)      ; higher byte of the string address
        push r25
        push r24
        push r23
        push r22
        call printf
        pop r22
        pop r23
        pop r24
        pop r25

        ret
\end{lstlisting}


Everything up to the main is what we're used to. Note, however, that the \verb!values! label doesn't have any \verb!.byte! directives---all this means is that the memory address corresponding to \verb!values! hasn't been initialized.

In this example, we'll be using the \verb!Y! register pointer (there's no particular reason why we're using \verb!Y!---we could have used \verb!X! or \verb!Z! instead), which corresponds to the registers \verb!r28! and \verb!r29!. So, we save the contents of these registers, and we load the low and high byte of \verb!r18! (which stores $77$) into them. On Line $26$, we load what \verb!Y! is pointing to into \verb!r24!, and increment \verb!Y! by one (so \verb!Y! is now pointing to a new uninitialized area of memory). Next, the value $77$ is printed by calling \verb!pint!.

Line $30$ updates the contents of \verb!r18! to $99$, and line \verb!31! stores $99$ into \verb!Y!. Note that we use \verb!st!, which works with register pointers, instead of \verb!sts!. Lines $32$ to $33$ initialize the register pointer \verb!Z!, and Line $34$ moves \verb!Z! forward by one (\verb!Z! is now pointing to $99$). Thus, loading this value into \verb!r24! and calling \verb!pint! prints out $99$.

\subsection{Instruction Encoding and the Status Register}

In Assembly, an instruction is represented by a set of bits which are assembled into a set of zeros and ones. But, not all of these bits are telling the assembler what operation to perform. The portion of the bits that encode what operation should be performed is known as the \vocab{opcode}. 

So, what do the rest of the bits represent? The short answer is that it is dependent on the instruction we're considering. For instance, if we're dealing with \verb!ldi!, we'd have the opcode, another portion to store the registers, and another portion to store the values of the registers. 

The number of bits necessary to encode an instruction also varies. In AVR, however, we're guaranteed that instructions are either two bytes or four bytes. When memory is scarce, choosing the correct instruction is vital to saving memory.


In an Assembly program, there's a register---known as the \vocab{status register}---that keeps track of recent operations (particularly mathematical operations). We can view what's inside of the status register by typing \verb!info r! in gdb. Why is the status register important? It allows us to perform conditional executions, known as \vocab{branching}. 

\subsection{Branch Instructions}

We can use the status register to perform conditional execution of statements. This is done with the \verb!cp! instruction, which has syntax \verb!cp [register1] [register2]!. This is used to compare the contents of two registers.

Consider the following example:

\begin{lstlisting}
;;; Example - if a == b prints 'Y' else prints 'N'
;;; Change a and b to see different outputs

    .set LETTER_N, 'N'
    .set LETTER_Y, 'Y'

;;; Global data
    .data

a:      .byte 0x6              
b:      .byte 0x5

;;; Program code
        .text                 

.global main                  
main:                          
        call init_serial_stdio

        lds r18, a             ; reading a from memory  
        lds r19, b             ; reading b from memory
        cp r18, r19            ; comparing registers
        breq 1f                ; 1 is the target: f represents forward
        ldi  r24, LETTER_N     ; store N
        jmp 2f
1:      ldi r24, LETTER_Y      ; store Y

2:      clr r25                ; printing result
        call putchar          

        clr r25                ; print newline
        ldi r24, 10            
        call putchar
    
        cli                    ; stopping program
        sleep                          

        ret                   
\end{lstlisting}

In this program, we read the value of \verb!a! and \verb!b! into registers \verb!r18! and \verb!r19!. These values are compared with \verb!cp! on Line $22$, which has syntax \verb!cp [register1], [register2]!. The instruction \verb!breq! is short for ``branch if equal,'' and its syntax is \verb!breq [label]!. Pretty much, this instruction utilizes the status register to check the value of the preceding comparison. If the comparison indicated that the two variables were equal, the program jumps to the target label (here, the \verb!f! indicates that we're jumping forward). 

Suppose $a \neq b$. In this case, we don't jump to label $1$. Instead, we'll store the letter \verb!N! in \verb!r24!. The next instruction, \verb!jmp!, indicates an unconditional jump (it always gets executed). Hence, once \verb!N! is loaded into \verb!r24!, we'll jump forward to label $2$, and we'll print the result along with a new line.


Now suppose $a = b$ holds. In this case, we'll jump over the statement that loads \verb!N! into \verb!r24!. Instead, we'll load \verb!Y! into \verb!r24!, and we'll continue from there and print the character along with a new line.


The advantage of using \verb!cp! is that we don't have to modify the registers.

Some other branch instructions are listed below: \begin{itemize}
    \item \verb!cpi [register], [constant]! compares the contents of a register to a constant.
    \item \verb!tst [register]! tests whether a register is non-positive.
    \item \verb!breq [label]! branches to \verb!label! if the previous comparison indicated an equality.
    \item \verb!brne [label]! branches to \verb!label! if the previous comparison did not indicate an equality.
    \item \verb!brge [label]! branches to \verb!label! if the previous comparison indicated \verb!register1! was greater than or equal to \verb!register2!. This should be used on signed integers.
    \item \verb!brlt [label]! branches if \verb!register1! is strictly less than \verb!register2!. This instruction should also be used for signed integer.
\end{itemize}

It's important to remember that branching instructions always look at the last result with the status register. Thus, comparison instructions need to come immediately before the branch instructions.


Here's another example.

\lstset{
caption=Assembly: Do-While Loop
}

\begin{lstlisting}
;;; Example - prints values 1 to 5 (do while)

;;; Global data
    .data

pctd:
        .asciz "%d "         

upper_limit: .byte 0x5              

;;; Program code
        .text                 

.global main                  
main:                          
        call init_serial_stdio

        push r15               ; callee-save 
        push r16               

        lds r15, upper_limit   ; upper limit
        ldi r16,1              ; loop starts, r16 is the iteration variable

1:      mov r24, r16           ; printing value
        clr r25                
        call pint

        inc r16                ; increasing iteration variable
        cp r15, r16           ; checking whether we reach limit
        brge 1b                ; go back as long as r15 >= r16

        clr r25                ; newline
        ldi r24, 10            
        call putchar
    
        pop r16                ; restoring registers
        pop r15
    cli                    ; stopping program
        sleep                          

        ret                    

pint:
        ;; prints an integer value, r22/r23 have the format string
        ldi r22, lo8(pctd)      ; lower byte of the string address
        ldi r23, hi8(pctd)      ; higher byte of the string address
        push r25
        push r24
        push r23
        push r22
        call printf
        pop r22
        pop r23
        pop r24
        pop r25

        ret
\end{lstlisting}

There isn't anything confusing here. Register \verb!r15! stores the upper limit of loop. After each iteration. we compare the iteration variable, stored in \verb!r16!, to the upper limit. If we haven't hit the limit, we branch back to the start of the loop. Similarly, we could implement a while loop by performing an initial comparison.